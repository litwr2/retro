TED:            CMP     [TICKS2TED],0           ;free .L25, ...
                JZ      TED

                DEC     [TICKS2TED]
                INC     [TOTALTICKS]
                ;TEST    BYTE [RETRACE_COUNT],14  ;frame skipping
                ;JNZ     .L14

                CMP     [TED_1E],36H    ;DMA FETCH START
                JNE     .L2

                CMP     [ISDMANOW],1
                JB      .L2

                JA      .L12

                MOV     BX,[DMA_BASE]
                ADD     BH,[VM_BASE]
                MOV     CX,10
                XOR     SI,SI
.ATTR:          MOV     EAX,[FS:BX+SI]
                MOV     DWORD [VID_BUF1+SI],EAX
                ADD     SI,4
                LOOP    .ATTR
                JMP     .L2

.L12:           MOV     BX,[DMA_BASE]
                XOR     SI,SI
                MOV     CX,10
                ADD     BH,[VM_BASE]
.CHAR:          MOV     EAX,[FS:BX+SI+400H]
                MOV     DWORD [VID_BUF2+SI],EAX
                MOV     EAX,DWORD [VID_BUF1+SI]
                MOV     DWORD [VID_BUF3+SI],EAX
                ADD     SI,4
                LOOP    .CHAR

.L2:            CMP     [TED_1E],SHVA   ;HP<=EHVA || HP>=SHVA
                JAE     .L9             ;EMULATOR SCREEN FRAME

                CMP     [TED_1E],EHVA
                JA      .COUNTERS

.L9:            CMP     [BEAMY],EVVA
                JA      .COUNTERS

                CMP     [ISDMA],0               ;DMA?
                JE      .BORDER

                CMP     [ISDMA],2
                JE      .L2A

                MOV     DI,[VD_PTR_BEAM]        ;EXTRA
                ADD     EDX,[TOTALTICKS]
                MOV     [DI],EDX
                MOV     [DI+4],EDX
                JMP     .L2B

.L2A:           MOV     SI,[CP_CURRENT]
                SUB     SI,WORD [TED_1A]
                LGS     BX,[MDATA_ADDR]
                MOV     DL,[VID_BUF2+SI]        ;DH-ATTR, DL-CHAR
                MOV     DH,[VID_BUF3+SI]
                MOV     SI,[VMODE]
                MOV     DI,[VD_PTR_FILL]
                CALL    [VMJT+SI]
                MOV     [VD_PTR_FILL],DI
                INC     [CP_CURRENT]

.L2B:           MOV     AX,WORD [TED_1C]
                CMP     AX,[EVSA]
                JA      .BORDER

                CMP     AX,[SVSA]
                JB      .BORDER

        ;cmp ax,160
        ;je  .BORDER
                MOV     AL,[TED_1E]
                CMP     AL,[SHSA]
                JE      .SHOW8

                CMP     AL,[EHSA]
                JBE     .SHOW8

.BORDER:        MOV     EAX,[BORDER_COLOR]
                MOV     DI,[VD_PTR_BEAM]
                MOV     [DI],EAX
                MOV     [DI+4],EAX

.SHOW8:         LES     DI,[BEAM_ADDR]
                ADD     [BEAM_POS],8

                MOV     SI,[VD_PTR_BEAM]
                MOVSD
                MOVSD
                MOV     [VD_PTR_BEAM],SI

.COUNTERS:      CMP     [ISDMA],2
                JNE     .L14

                CMP     [TED_1E],34H
                JNE     .L7

                INC     [VSA]
                AND     [VSA],7
                JMP     .L23

.L7:            CMP     [TED_1E],35H
                JNE     .L6

.L16:           MOV     AX,WORD [TED_1A]
                MOV     [CP_CURRENT],AX
                JMP     .L23

.L6:            CMP     [TED_1E],28H
                JNE     .L5

                CMP     [VSA],6
                JNE     .L23

                ADD     [DMA_BASE],40
                JMP     .L23

.L5:            CMP     [TED_1E],26H
                JNE     .L14

                CMP     [VSA],7
                JNE     .L23

                MOV     AX,[CP_CURRENT]
                MOV     WORD [TED_1A],AX
                JMP     .L23

.L14:           CMP     [TED_1E],29H
                JNE     .L25

                CMP     [ISDMANOW],0
                JE      .L23

                MOV     AL,[TED_1C]
                AND     AL,7
                CMP     AL,BYTE [Y_OFFSET]
                JNE     .L23

                MOV     [ISDMANOW],1
                JMP     .L23

.L25:           CMP     [TED_1E],33H
                JNE     .L8

                MOV     [CLADDR],LBLAF
                MOV     AX,WORD [TED_1C]
                TEST    [TED_13],2      ;SINGLE CLOCK MODE?
                JZ      .L3

                MOV     [CLADDR],LBLAS
.L3:            CMP     AX,203
                JE      .L22

                JB      .L4

                CMP     AX,204
                JA      .L4

                MOV     [ISDMA],0
                MOV     [ISDMANOW],0
                JMP     .L23

.L22:           MOV     [ISDMANOW],0
                CMP     [ISDMA],0
                JE      .L23

                MOV     [CLADDR],LNORMF
                TEST    [TED_13],2      ;SINGLE CLOCK MODE?
                JZ      .L23

                MOV     [CLADDR],LNORMS
                JMP     .L23

.L4:            CMP     [ISDMA],0
                JE      .L23

                MOV     [CLADDR],LNORMF
                TEST    [TED_13],2      ;SINGLE CLOCK MODE?
                JZ      .L1

                MOV     [CLADDR],LNORMS
.L1:            AND     AL,7
                CMP     AL,BYTE [Y_OFFSET]
                JNE     .L12H

.L26:           MOV     [ISDMANOW],1
                JMP     .L10

.L12H:          CMP     [ISDMANOW],1
                JB      .L23

                MOV     [ISDMANOW],0
                JA      .L23

                MOV     [ISDMANOW],2
                MOV     [ISDMA],2
.L10:           MOV     [CLADDR],LDMAF
                TEST    [TED_13],2      ;SINGLE CLOCK MODE?
                JZ      .L23

                MOV     [CLADDR],LDMAS
                JMP     .L23

.L8:            CMP     [TED_1E],37H
                JNE     .L19

                MOV     AX,VIDEO_DATA   ;???
                MOV     [VD_PTR_BEAM],AX
                ADD     AX,[X_OFFSET]
                MOV     [VD_PTR_FILL],AX
.L19:           CMP     [TED_1E],31H
                JNE     .L23

                MOV     AX,WORD [TED_1C]
                INC     [BEAMY]
                INC     AX
                CMP     AX,MAX_VER              ;V-SYNC
                JBE     .L17

                INC     [RETRACE_COUNT]
                CALL    GET_JOYS
                CALL    SYS_KEYS
                CMP     [TOTALTICKS],4000000000
                JBE     .L24

                CALL    ADJUST_TICKS
.L24:           INC     [BLINK_COUNT]
                JNZ     .L18

                MOV     [BLINK_COUNT],0F0H
                XOR     [VMODE],20H
.L18:           XOR     AX,AX
                MOV     [BEAM_ADDR],VIDEO_SEG*10000H

                MOV     [VSA],7
                MOV     [ISDMA],AL
                MOV     [ISDMANOW],AL
                MOV     [CP_CURRENT],AX
                MOV     [DMA_BASE],AX
                MOV     WORD [TED_1A],AX
                MOV     [BEAMY],AX
                TEST    [TED_6],10H             ;SCREEN BLANK?
                JZ      .L17

                INC     [ISDMA]
.L17:           MOV     WORD [TED_1C],AX
                CMP     [BEAM_POS],BEAM_CH_POS
                JB      .L17A

                SUB     [BEAM_POS],BEAM_CH_POS
                ADD     [BEAM_POS+2],BEAM_CH_POS/16
.L17A:          MOV     BX,WORD [TED_A]
                XCHG    BL,BH
                AND     BH,1

                CMP     AX,BX                   ;RASTER INTERRUPT?
                JNZ     .L23

                OR      [TED_9],2
                TEST    [TED_A],2
                JZ      .L23

                OR      [TED_9],80H
                MOV     BX,[CPUOP]
                MOV     [IRQOP],BX
.L23:           DEC     WORD [TED_0]            ;TIMERS INTERRUPT?
                JNZ     .L20

                MOV     AX,[TIMER1_RELOAD]
                MOV     WORD [TED_0],AX
                OR      [TED_9],8
                TEST    [TED_A],8
                JZ      .L20

                OR      [TED_9],80H
                MOV     AX,[CPUOP]
                MOV     [IRQOP],AX
.L20:           DEC     WORD [TED_2]
                JNZ     .L21

                OR      [TED_9],10H
                TEST    [TED_A],10H
                JZ      .L21

                OR      [TED_9],80H
                MOV     AX,[CPUOP]
                MOV     [IRQOP],AX
.L21:           DEC     WORD [TED_4]
                JNZ     .L11

                OR      [TED_9],40H
                TEST    [TED_A],40H
                JZ      .L11

                OR      [TED_9],80H
                MOV     AX,[CPUOP]
                MOV     [IRQOP],AX
.L11:           MOVZX   BX,[TED_1E]
                MOV     SI,[CLADDR]
                MOV     AL,[BX+SI]
                MOV     [TICKS],AL
                INC     [TED_1E]
                CMP     [TED_1E],38H
                JBE     .L15

                MOV     [TED_1E],0
.L15:           OR      AL,AL
                JE      TED

                RETN

T00xxx:         ADD     DI,8
                RETN

T00100:         MOV     SI,DX           ;*** MCM+
                AND     SI,7FH
                BTR     DX,11
                JC      T01100.L1

                MOV     CH,[GS:EBX+ESI*8]       ;MATRIX LINE
                OR      DL,DL
                JNS     T00000.L0A

                JMP     T00000.L0B

T01100:         BTR     DX,11           ;*** MCM+ HRV+
                JNC     T01000

                MOVZX   SI,DL
.L1:            MOV     CH,[GS:EBX+ESI*8]       ;MATRIX LINE
                MOV     AX,[COLOR1]             ;SI-0, AX-1, BX-2, DX-3
                MOV     BX,[COLOR2]
                MOV     DL,DH
                JMP     T00101.L0

T01000:         MOVZX   SI,DL           ;*** HRV+
                MOV     CH,[GS:EBX+ESI*8]       ;MATRIX LINE
                JMP     T00000.L0A

T00000:         MOV     SI,DX           ;***
                AND     SI,7FH
                MOV     CH,[GS:EBX+ESI*8]       ;MATRIX LINE
                OR      DL,DL
                JNS     .L0A

.L0B:           NOT     CH
.L0A:           MOV     CL,[TED_15]
.L0:            TEST    CH,16
                MOV     AL,CL
                JZ      .L1

                MOV     AL,DH
.L1:            TEST    CH,32
                MOV     AH,CL
                JZ      .L2

                MOV     AH,DH
.L2:            BSWAP   EAX
                TEST    CH,64
                MOV     AH,CL
                JZ      .L3

                MOV     AH,DH
.L3:            TEST    CH,128
                MOV     AL,CL
                JZ      .L4

                MOV     AL,DH
.L4:            MOV     [DI],EAX
                TEST    CH,1
                MOV     AL,CL
                JZ      .L5

                MOV     AL,DH
.L5:            TEST    CH,2
                MOV     AH,CL
                JZ      .L6

                MOV     AH,DH
.L6:            BSWAP   EAX
                TEST    CH,4
                MOV     AH,CL
                JZ      .L7

                MOV     AH,DH
.L7:            TEST    CH,8
                MOV     AL,CL
                JZ      .L8

                MOV     AL,DH
.L8:            MOV     [DI+4],EAX
                ADD     DI,8
                RETN

T11000:         OR      DH,DH                   ;*** HRV+ FLASH+
                JS      V1_00

                MOV     SI,[CP_CURRENT]
                CMP     SI,WORD [TED_C]
                JNZ     T01000

                MOVZX   SI,DL
                MOV     CH,[GS:EBX+ESI*8]       ;MATRIX LINE
                NOT     CH
                JMP     T00000.L0A

V1_00:          MOV     AX,[COLOR0]
                MOV     [DI],AX
                MOV     [DI+2],AX
                MOV     [DI+4],AX
                MOV     [DI+6],AX
                ADD     DI,8
                RETN

T10000:         OR      DH,DH                   ;*** FLASH+
                JS      V1_00

                MOV     SI,DX
                AND     SI,7FH
                MOV     CH,[GS:EBX+ESI*8]       ;MATRIX LINE
                MOV     SI,[CP_CURRENT]
                CMP     SI,WORD [TED_C]
                JNZ     .L1

                NOT     CH
.L1:            OR      DL,DL
                JS      T00000.L0B

                JMP     T00000.L0A

T00010:         MOV     SI,DX                   ;*** ECM+
                AND     SI,3FH
                MOV     CH,[GS:EBX+ESI*8]       ;MATRIX LINE
                MOVZX   BX,DL
                SHR     BL,6
                MOV     CL,[TED_15+BX]          ;BACKGROUND
                JMP     T00000.L0

T00001:         MOV     SI,[CP_CURRENT]         ;*** BMM+
                MOV     CH,[GS:EBX+ESI*8]       ;MATRIX LINE
                MOV     AL,DH
                MOV     DH,DL
                MOV     AH,AL
                AND     DX,0FF0H
                AND     AX,7007H
                OR      AL,DL
                OR      DH,AH           ;BACKGROUND
                ROL     AL,4            ;FOREGROUND
                XCHG    AL,DH
                MOV     CL,AL
                JMP     T00000.L0

T00101:         MOV     SI,[CP_CURRENT]         ;*** BMM+ MCM+
                MOV     CH,[GS:EBX+ESI*8]       ;MATRIX LINE
                MOV     AL,DH
                MOV     DH,DL
                MOV     AH,AL
                AND     DX,0FF0H
                AND     AX,7007H
                OR      AL,DL
                OR      DH,AH           ;COLOR #2
                ROL     AL,4            ;COLOR #1
                MOV     AH,AL
                MOV     DL,DH
                MOV     BX,[COLOR1]     ;SI-0, AX-1, BX-2, DX-3
                XCHG    BX,DX

.L0:            MOV     SI,[COLOR0]
                MOV     CL,4
.L1:            SHL     CH,1
                JNC     .L0X

                SHL     CH,1
                JNC     .L10

                MOV     [DI],DX         ;11
                JMP     .LE

.L0X:           SHL     CH,1
                JNC     .L00

                MOV     [DI],AX         ;01
                JMP     .LE

.L10:           MOV     [DI],BX         ;10
                JMP     .LE

.L00:           MOV     [DI],SI         ;00
.LE:            ADD     DI,2

                LOOP    .L1
                RETN


