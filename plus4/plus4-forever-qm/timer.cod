SET_INTR8:      MOV     AX,3508H        ;SAVE/SET INTR8 VECTOR
                INT     21H
                PUSH    DS
                PUSH    CS
                POP     DS
                MOV     [SAVE8LO],BX
                MOV     [SAVE8HI],ES
                MOV     DX,INTR8SND
                MOV     AX,2508H
                INT     21H
                POP     DS

                CLI
                MOV     AL,36H          ;SET TIMER HARDWARE
                OUT     43H,AL
                MOV     AL,TIMERV AND 0FFH
                OUT     40H,AL
                MOV     AL,TIMERV SHR 8
                OUT     40H,AL
                STI
                RETN

RESTORE_INTR8:  MOV     DX,[CS:SAVE8LO]     ;RESTORE INTR8 VECTOR
                PUSH    DS
                MOV     DS,[CS:SAVE8HI]
                MOV     AX,2508H
                INT     21H
                POP     DS

                CLI
                MOV     AL,36H          ;RESTORE TIMER HARDWARE
                OUT     43H,AL
                XOR     AL,AL
                OUT     40H,AL
                OUT     40H,AL
                STI
                RETN

INTR8SND:       PUSH    AX
                PUSH    BX
                PUSH    DS
                MOV     AX,SEG_DATA
                MOV     DS,AX
        
                CMP     [TIMER_ST],0
                JNE     .L22

                INC     [SYNCHRO_TICKS]
.L22:           CMP     [SB_SOUND],0
                JNE     SB_SND

                IN      AL,61H
                AND     AL,0FDH
                CMP     [TIMER_SOUND],0
                JE      .L14

                MOV     BX,[SND1POS]
                TEST    [TED_11],10H
                JZ      .L8

                TEST    [TED_11],80H
                JNZ     .L7

                CMP     BX,[POS1SND1]
                JBE     .L4

.L7:            INC     BX
                OR      AL,2
                CMP     BX,[POS2SND1]
                JBE     .L6

                XOR     BX,BX
                JMP     .L6

.L4:            INC     BX
.L6:            MOV     [SND1POS],BX
.L8:            PUSH    AX
                MOV     BX,[SND2POS]
                TEST    [TED_11],60H
                JZ      .L8A

                TEST    [TED_11],20H
                JNZ     .L9

                PUSH    AX
                MOV     AX,[POS1SND2]
                ADD     AX,[NOISE_OFFSET]
                TEST    BYTE [TOTALTICKS],1
                JZ      .L11

                SHR     AX,1
.L11:           CMP     AX,[POS2SND2]
                JB      .L10

                SHR     AX,3
                JNZ     .L11

.L10:           MOV     [POS1SND2],AX
                POP     AX
.L9:            TEST    [TED_11],80H
                JNZ     .L7A

                CMP     BX,[POS1SND2]
                JA      .L4A

.L7A:           INC     BX
                OR      AL,2
                CMP     BX,[POS2SND2]
                JBE     .L6A

                XOR     BX,BX
                JMP     .L6A

.L4A:           INC     BX
.L6A:           MOV     [SND2POS],BX
.L8A:           POP     BX
                OR      AL,BL
                MOVZX   BX,[VOL]
                SHL     BX,4            ;*16
                LEA     BX,[TIMER_VOL+BX]
                PUSH    AX
                MOVZX   AX,BYTE [INTR8COUNT]
                AND     AL,15
                ADD     BX,AX
                POP     AX
                CMP     BYTE [BX],'0'
                JNE     .L12

.L14:           AND     AL,0FDH
.L12:           OUT     61H,AL
.L0:            INC     [INTR8COUNT]
                CMP     [INTR8COUNT],65536/TIMERV
                JA      .L1

                MOV     AL,20H
                OUT     20H,AL
                CMP     [TIMER_ST],0
                JNZ     .L23

                MOV     AX,[SPEED_LIMIT]
                ADD     [TICKS2TED],AX
.L23:           POP     DS
                POP     BX
                POP     AX
                IRET

.L1:            XOR     AX,AX
                MOV     [INTR8COUNT],AX
                POP     DS
                POP     BX
                POP     AX
                DB      0EAH
SAVE8LO         DW      0
SAVE8HI         DW      0

SPEAKER_0E:     PUSH    AX
                PUSH    BX
                PUSH    DX
                MOV     DL,[TED_12]
                XCHG    DL,DH
                CALL    SPEAKER_SFREQ
                MOV     [POS2SND1],AX
                SHR     AX,1
                MOV     [POS1SND1],AX
                MOV     [SND1POS],0
                POP     DX
                POP     BX
                POP     AX
                RETN

SPEAKER_0F:     PUSH    AX
                PUSH    BX
                PUSH    DX
                MOV     DL,[TED_10]
                XCHG    DL,DH
                CALL    SPEAKER_SFREQ
                MOV     [POS2SND2],AX
                MOV     BX,AX
                SHR     AX,1
                MOV     [POS1SND2],AX
                SUB     BX,AX
                SHR     BX,3
                JNZ     .L1

                INC     BX
.L1:            MOV     [NOISE_OFFSET],BX
                MOV     [SND2POS],0
                POP     DX
                POP     BX
                POP     AX
                RETN

SPEAKER_10:     PUSH    AX
                PUSH    BX
                PUSH    DX
                MOV     DL,[TED_F]
                CALL    SPEAKER_SFREQ
                MOV     [POS2SND2],AX
                SHR     AX,1
                MOV     [POS1SND2],AX
                MOV     [SND2POS],0
                POP     DX
                POP     BX
                POP     AX
                RETN

SPEAKER_11:     PUSH    AX
                MOV     AL,DH
                AND     AL,0FH
                CMP     AL,8
                JBE     .L1

                MOV     AL,8
.L1:            MOV     [VOL],AL
                TEST    DH,10H
                JNZ     .L2

                MOV     [SND1POS],0
.L2:            TEST    DH,20H
                JNZ     .L3

                MOV     [SND2POS],0
                JMP     .L4

.L3:            MOV     AX,[POS2SND2]
                SHR     AX,1
                MOV     [POS1SND2],AX
.L4:            TEST    [TED_11],80H
                JZ      .L5

                MOV     [SND1POS],0
                MOV     [SND2POS],0
.L5:            POP     AX
                RETN

SPEAKER_12:     PUSH    AX
                PUSH    BX
                PUSH    DX
                MOV     DL,[TED_E]
                CALL    SPEAKER_SFREQ
                MOV     [POS2SND1],AX
                SHR     AX,1
                MOV     [POS1SND1],AX
                MOV     [SND1POS],0
                POP     DX
                POP     BX
                POP     AX
                RETN

SPEAKER_SFREQ:;  INC     DX
                AND     DH,3            ;DX - COMMODORE SOUND FREQ DETERMINANT

                MOV     BX,1024
                SUB     BX,DX
                MOV     EAX,SOUND_CONST
                XOR     EDX,EDX
                DIV     EBX
                SHL     DX,1
                CMP     BX,DX
                JA      .L1

                INC     EAX
.L1:            MOV     EBX,EAX
                XOR     EDX,EDX
                MOV     EAX,1193180/TIMERV
                DIV     EBX
                CMP     EBX,EDX
                JA      .L2

                INC     AX
.L2:            XOR     EBX,EBX         ;AX - POS2
                RETN

INIT_SPEAKER:   INC     [TIMER_SOUND]
                RETN

EXIT_SPEAKER:   MOV     [TIMER_SOUND],0
                IN      AL,61H
                AND     AL,0FDH
                OUT     61H,AL
                RETN

