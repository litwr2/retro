RESTORE_INTR9:  MOV     DX,CS:SAVE9LO    ;RESTORE INTR9 VECTOR
                PUSH    DS
                MOV     DS,CS:SAVE9HI
                JMP     SHORT SI9_L1

SET_INTR9:      MOV     AX,3509H        ;SAVE/SET INTR9 VECTOR
                INT     21H
                MOV     CS:SAVE9LO,BX
                MOV     CS:SAVE9HI,ES
                PUSH    DS
                PUSH    CS
                POP     DS
                LEA     DX,CS:INTR9
SI9_L1:         MOV     AX,2509H
                INT     21H
                POP     DS
                RETN

PREP8042:       MOV     AL,0EDH
                OUT     60H,AL
                MOV     AH,AL
P8042_L1:       IODELAY
                DEC     AX
                JNZ     SHORT P8042_L1
                RETN

SET_LED:        PUSHF
                CLI
                CALL    PREP8042
                MOV     AL,LED
                PUSH    DS
                PUSH    0
                POP     DS
                AND     BYTE PTR DS:[417H],8FH
                OR      DS:[417H],AL
                POP     DS
                SHR     AL,4
                OUT     60H,AL
                POPF
                RETN

INTR9:          PUSH    AX
                PUSH    DS
                MOV     AX,SEG SEG_DATA
                MOV     DS,AX
                IN      AL,60H
                MOV     AH,AL
                XCHG    AH,PREV_KEY

                CMP     AL,0E0H
                JAE     I9_LEZ

                CMP     AH,0E0H
                JNZ     SHORT I9_L2
                MOV     AH,AL
                AND     AH,7FH
                CMP     AH,2AH
                JZ      I9_LEZ
                CMP     AH,36H
                JZ      I9_LEZ

I9_L2:          PUSH    BX
                PUSH    DX
                CMP     AL,0C6H         ;ScrollLock?
                JNZ     SHORT I9_L4
                XOR     LED,10H
                TEST    LED,10H
                JNZ     SHORT I9_L7
                AND     LED,0DFH
I9_L7:          CALL    SET_LED
                JMP     SHORT I9_LE

I9_L4:          CMP     AL,0D4H         ;SysReq (Alt+PrintScreen)?
                JZ      I9_SR

                CMP     AL,0C5H         ;NumLock?
                JZ      SHORT I9_LNL

                CMP     AL,0BAH         ;CapsLock?
                JZ      SHORT I9_LCL

                CMP     INTR9_ST,'X'    ;debugger mode?
                JZ      SHORT I9_LX

                CMP     AL,3AH          ;CapsLock Pressed?
                JZ      SHORT I9_LX1

                MOV     DL,AL
                AND     AX,7FH
                MOV     BX,AX
                MOV     DH,AL
                OR      AH,KT1[BX]
                JS      SHORT I9_L3

                MOV     BL,AH
                MOV     AL,AH
                SHR     BL,3
                AND     AX,7
                OR      DL,DL
                JS      SHORT I9_L1

                BTR     WORD PTR KMATRIX[BX],AX
                JMP     SHORT I9_LE

I9_L1:          BTS     WORD PTR KMATRIX[BX],AX
                TEST    LED,40H
                JZ      SHORT I9_L3
                AND     KMATRIX+1,07FH
I9_L3:          MOV     KBUFA,AH
                MOV     WORD PTR KBUFS,DX

I9_LE:          POP     DX
                POP     BX
I9_LEZ:         POP     DS
                MOV     AL,20H
                OUT     20H,AL
                POP     AX
                IRET

I9_LNL:         XOR     LED,20H
                MOV     AL,LED
                AND     AL,20H
                SHR     AL,1
                AND     LED,0EFH
                OR      LED,AL
                CALL    SET_LED
                JMP     SHORT I9_LE

I9_LCL:         XOR     LED,40H
                XOR     KMATRIX+1,80H
                JMP     SHORT I9_LX1

I9_LX:          CMP     AL,046H         ;ScrollLock?
                JZ      SHORT I9_LE
                CMP     AL,054H         ;SysReq (Alt+PrintScreen)?
                JZ      SHORT I9_SR
                CMP     AL,045H         ;NumLock?
                JZ      SHORT I9_LE
I9_LX1:         POP     DX
                POP     BX
                POP     DS
                POP     AX
                DB      0EAH
SAVE9LO         DW      ?
SAVE9HI         DW      ?

I9_SR:          XCHG    BP,SP
                CMP     WORD PTR [BP+10],SEG SEG_CODE
                JNZ     SHORT I9_L4B
                MOV     WORD PTR [BP+8],OFFSET END_EMU
                MOV     DWORD PTR FMSG+12,'oba '
                MOV     WORD PTR FMSG+16,'tr'
I9_L4B:         XCHG    BP,SP
                JMP     SHORT I9_LE

SYS_KEYS:       TEST    KBUFA,40H
                JZ      SHORT SKS_L4

                XOR     AX,AX
                TEST    INTR9_ST,AL
                JNZ     SHORT SKS_L4
                XCHG    AX,WORD PTR KBUFS

                CMP     AL,0C3H         ;F9=DEBUGGER
                JNZ     SHORT SKS_L1
                MOV     MAINJUMP,OFFSET DEBUG_ENTRY

SKS_L1:         CMP     AL,0D7H         ;F11=RESET
                JNZ     SHORT SKS_L2
                MOV     MAINJUMP,OFFSET RESET

SKS_L2:         CMP     AL,0D8H         ;F12=MENUS
                JNZ     SHORT SKS_L3

                TEST    CPU_1,8
                JNZ     SHORT SKS_L5
                TEST    LED,10H
                JNZ     SHORT SKS_L4
SKS_L5:         MOV     MAINJUMP,OFFSET MENUS_ENTRY

SKS_L3:         CMP     AL,0C4H         ;F10=EXIT
                JNZ     SHORT SKS_L4
                MOV     MAINJUMP,OFFSET END_EMU
SKS_L4:         RETN
