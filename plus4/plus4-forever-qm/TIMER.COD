SET_INTR8:      MOV     AX,3508H        ;SAVE/SET INTR8 VECTOR
                INT     21H
                MOV     [CS:SAVE8LO],BX
                MOV     [CS:SAVE8HI],ES
                PUSH    DS
                PUSH    CS
                POP     DS
                MOV     DX,INTR8SND
                MOV     AX,2508H
                INT     21H
                POP     DS
                MOV     AL,36H          ;SET TIMER HARDWARE
                OUT     43H,AL
                MOV     AL,TIMERV
                OUT     40H,AL
                XOR     AL,AL
                OUT     40H,AL
                RETN

RESTORE_INTR8:  MOV     DX,[CS:SAVE8LO]     ;RESTORE INTR8 VECTOR
                PUSH    DS
                MOV     DS,[CS:SAVE8HI]
                MOV     AX,2508H
                INT     21H
                POP     DS

                MOV     AL,36H          ;RESTORE TIMER HARDWARE
                OUT     43H,AL
                XOR     AL,AL
                OUT     40H,AL
                OUT     40H,AL
                RETN

INTR8SND:       PUSH    AX
                PUSH    BX
                PUSH    DS
                MOV     AX,SEG SEG_DATA
                MOV     DS,AX
        
                CMP     [TIMER_ST],0
                JNE     .L22
                INC     [SYNCHRO_TICKS]

.L22:           IN      AL,61H
                AND     AL,0FDH

                CMP     [TIMER_SOUND],0
                JE      .L14

                MOV     BX,[SND1POS]

                TEST    [TED_11],10H
                JZ      .L8

                TEST    [TED_11],80H
                JNZ     .L7

                CMP     BX,[POS1SND1]
                JA      .L4

.L7:            INC     BX

                OR      AL,2
                JMP     .L6

.L4:            INC     BX
                CMP     BX,[POS2SND1]
                JBE     .L6

.L5:            XOR     BX,BX

.L6:            MOV     [SND1POS],BX
.L8:            PUSH    AX
                MOV     BX,[SND2POS]

                TEST    [TED_11],60H
                JZ      .L8A

                TEST    [TED_11],20H
                JNZ     .L9

                PUSH    AX
                MOV     AX,[POS1SND2]
                ADD     AX,[NOISE_OFFSET]
                TEST    BYTE [TOTALTICKS],1
                JZ      .L11
                SHR     AX,1

.L11:           CMP     AX,[POS2SND2]
                JB      .L10
                SHR     AX,3
                JNZ     .L11

.L10:           MOV     [POS1SND2],AX
                POP     AX

.L9:            TEST    [TED_11],80H
                JNZ     .L7A

                CMP     BX,[POS1SND2]
                JA      .L4A

.L7A:           INC     BX

                OR      AL,2
                JMP     .L6A

.L4A:           INC     BX
                CMP     BX,[POS2SND2]
                JBE     .L6A

.L5A:           XOR     BX,BX

.L6A:           MOV     [SND2POS],BX
.L8A:           POP     BX
                OR      AL,BL
                MOV     BL,BYTE [INTR8COUNT]
                MOV     BH,[VOL]

                AND     BL,15
                CMP     BH,8
                JZ      .L12
                OR      BH,BH
                JZ      .L14
                CMP     BL,7
                JZ      .L14
                CMP     BL,15
                JZ      .L14
                CMP     BH,7            ;SKIP (7,15)
                JZ      .L12
                CMP     BH,6            ;SKIP 3,11(,7,15)
                JNZ     .L16
                CMP     BL,3
                JZ      .L14
                CMP     BL,11
                JZ      .L14
                JMP     .L12
.L16:           CMP     BH,5            ;SKIP 3,11,5,13(,7,15)
                JNZ     .L17
                CMP     BL,3
                JZ      .L14
                CMP     BL,5
                JZ      .L14
                CMP     BL,11
                JZ      .L14
                CMP     BL,13
                JZ      .L14
                JMP     .L12
.L17:           CMP     BH,4            ;SKIP 1,9,3,11,5,13(,7,15)
                JNZ     .L15
                TEST    BL,1
                JNZ     .L14
                JMP     .L12
.L15:           CMP     BH,3            ;SKIP 0,8,2,10,3,11,5,13(,7,15)
                JNZ     .L18
                CMP     BL,1
                JZ      .L12
                CMP     BL,4
                JZ      .L12
                CMP     BL,6
                JZ      .L12
                CMP     BL,9
                JZ      .L12
                CMP     BL,12
                JZ      .L12
                CMP     BL,14
                JZ      .L12
                JMP     .L14
.L18:           CMP     BH,2            ;SKIP 0,1,9,3-5,11-13(,7,15)
                JNZ     .L19
                CMP     BL,1
                JZ      .L12
                CMP     BL,5
                JZ      .L12
                CMP     BL,8
                JZ      .L12
                CMP     BL,11
                JZ      .L12
                CMP     BL,14
                JZ      .L12
                JMP     .L14
.L19:           TEST    BL,3            ;SKIP 1-3,9-11,5-6,13-14(,7,15)
                JZ      .L12
.L14:           AND     AL,0FDH
.L12:           OUT     61H,AL

                INC     [INTR8COUNT]
                CMP     [INTR8COUNT],65536/TIMERV
                JA      .L1

                MOV     AL,20H
                OUT     20H,AL

                CMP     [TIMER_ST],0
                JNZ     .L23

                MOV     AX,[SPEED_LIMIT]
                ADD     [TICKS2TED],AX

.L23:           POP     DS
                POP     BX
                POP     AX
                IRET

.L1:            XOR     AX,AX
                MOV     [INTR8COUNT],AX
                POP     DS
                POP     BX
                POP     AX
                DB      0EAH
SAVE8LO         DW      0
SAVE8HI         DW      0

SPEAKER_0E:     PUSH    AX
                PUSH    BX
                PUSH    DX
                MOV     DL,[TED_12]
                XCHG    DL,DH
                CALL    SPEAKER_SFREQ
                MOV     [POS2SND1],AX
                SHR     AX,1
                MOV     [POS1SND1],AX
                MOV     [SND1POS],0
                POP     DX
                POP     BX
                POP     AX
                RETN

SPEAKER_0F:     PUSH    AX
                PUSH    BX
                PUSH    DX
                MOV     DL,[TED_10]
                XCHG    DL,DH
                CALL    SPEAKER_SFREQ
                MOV     [POS2SND2],AX
                MOV     BX,AX
                SHR     AX,1
                MOV     [POS1SND2],AX
                SUB     BX,AX
                SHR     BX,3
                JNZ     .L1
                INC     BX
.L1:            MOV     [NOISE_OFFSET],BX
                MOV     [SND2POS],0
                POP     DX
                POP     BX
                POP     AX
                RETN

SPEAKER_10:     PUSH    AX
                PUSH    BX
                PUSH    DX
                MOV     DL,[TED_F]
                CALL    SPEAKER_SFREQ
                MOV     [POS2SND2],AX
                SHR     AX,1
                MOV     [POS1SND2],AX
                MOV     [SND2POS],0
                POP     DX
                POP     BX
                POP     AX
                RETN

SPEAKER_11:     PUSH    AX
                MOV     AL,DH
                AND     AL,0FH
                CMP     AL,8
                JBE     .L1
                MOV     AL,8
.L1:            MOV     [VOL],AL

                TEST    DH,10H
                JNZ     .L2
                MOV     [SND1POS],0
.L2:            TEST    DH,20H
                JNZ     .L3
                MOV     [SND2POS],0
                JMP     .L4

.L3:            MOV     AX,[POS2SND2]
                SHR     AX,1
                MOV     [POS1SND2],AX

.L4:            TEST    [TED_11],80H
                JZ      .L5
                MOV     [SND1POS],0
                MOV     [SND2POS],0

.L5:            POP     AX
                RETN

SPEAKER_12:     PUSH    AX
                PUSH    BX
                PUSH    DX
                MOV     DL,[TED_E]
                CALL    SPEAKER_SFREQ
                MOV     [POS2SND1],AX
                SHR     AX,1
                MOV     [POS1SND1],AX
                MOV     [SND1POS],0
                POP     DX
                POP     BX
                POP     AX
                RETN

SPEAKER_SFREQ:;  INC     DX
                AND     DH,3            ;DX - COMMODORE SOUND FREQ DETERMINANT

                MOV     BX,1024
                SUB     BX,DX
                MOV     EAX,SOUND_CONST
                XOR     EDX,EDX
                DIV     EBX
                SHL     DX,1
                CMP     BX,DX
                JA      .L1
                INC     EAX
.L1:            MOV     EBX,EAX
                XOR     EDX,EDX
                MOV     EAX,1193180/TIMERV
                DIV     EBX
                CMP     EBX,EDX
                JA      .L2
                INC     AX
.L2:            XOR     EBX,EBX         ;AX - POS2
                RETN

INIT_SPEAKER:   INC     [TIMER_SOUND]
                RETN

EXIT_SPEAKER:   DEC     [TIMER_SOUND]
                IN      AL,61H
                AND     AL,0FDH
                OUT     61H,AL
                RETN

